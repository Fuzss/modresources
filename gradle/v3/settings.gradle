dependencyResolutionManagement {
    repositories {
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
    }

    versionCatalogs {
        libs {
            from("fuzs.sharedcatalogs:sharedcatalogs:${dependenciesVersionCatalog}")
            // Apply version overrides from gradle.properties
            settings.ext.properties.each { key, value ->
                // Extract catalog key: remove prefix and convert dots to dashes
                // Then set the catalog value (creates or overrides when already existing)
                switch (key) {
                    case { it.startsWith("project.libs.versions.") }:
                        version(key.substring("project.libs.versions.".length()).replace(".", "-"), value)
                        break

                    case { it.startsWith("project.libs.libraries.") }:
                        library(key.substring("project.libs.libraries.".length()).replace(".", "-"), value)
                        break

                    case { it.startsWith("libs.versions.") }:
                        version(key.substring("libs.versions.".length()).replace(".", "-"), value)
                        break

                    case { it.startsWith("libs.libraries.") }:
                        library(key.substring("libs.libraries.".length()).replace(".", "-"), value)
                        break
                }
            }
        }
    }
}

rootProject.name = "${modName.replaceAll("[^a-zA-Z]", "")}-${dependenciesVersionCatalog.substring(0, value.lastIndexOf('-'))}"
