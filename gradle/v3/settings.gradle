dependencyResolutionManagement {
    repositories {
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
    }

    versionCatalogs {
        libs {
            from("fuzs.sharedcatalogs:sharedcatalogs:${dependenciesVersionCatalog}")

            // Apply version overrides from gradle.properties
            settings.ext.properties.each { propName, propValue ->

                // Ignore unrelated properties
                if (propName.startsWith("libs.versions.")) {

                    // Extract catalog key: remove prefix and convert dots to dashes
                    def catalogKey = propName.substring("libs.versions.".length()).replaceAll(/\./, "-")

                    // Set the version (creates or overrides existing version)
                    version(catalogKey, propValue)
                }
            }
        }
    }
}

rootProject.name = "${modName.replaceAll("[^a-zA-Z]", "")}-${dependenciesVersionCatalog.replaceAll("-v\\d+", "")}"
