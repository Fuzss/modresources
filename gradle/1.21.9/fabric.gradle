apply plugin: "com.gradleup.shadow"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":Common").loom.accessWidenerPath

    runs {
        configureEach {
            ideConfigGenerated(true)
            runDir("../run")
            vmArgs("-Xms1G", "-Xmx4G", "-Dmixin.debug.export=true")
            vmArgs("-Dlog4j2.configurationFile=https://raw.githubusercontent.com/Fuzss/modresources/main/gradle/${libs.versions.minecraft.get()}/log4j.xml")
            vmArgs("-Dpuzzleslib.isDevelopmentEnvironment=true", "-D${modId}.isDevelopmentEnvironment=true")
            vmArgs("-Dfabric-tag-conventions-v2.missingTagTranslationWarning=silenced", "-Dfabric-tag-conventions-v1.legacyTagWarning=silenced")
        }
        client {
            client()
            setConfigName("Fabric Client ${libs.versions.minecraft.get()}")
        }
        server {
            server()
            setConfigName("Fabric Server ${libs.versions.minecraft.get()}")
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Ladysnake Libs"
        url = "https://maven.ladysnake.org/releases"
    }
    maven {
        name = "jamieswhiteshirt"
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
    }
}

dependencies {
    // Common Project
    common(project(path: ":Common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":Common", configuration: "transformProductionFabric")) { transitive false }

    // Fabric Loader
    modApi libs.fabricloader.fabric

    if (libs.hasProperty("modmenu") && libs.modmenu.hasProperty("fabric")) {
        modLocalRuntime(libs.modmenu.fabric) { transitive = false }
    }
}

tasks.withType(Jar).configureEach {
    exclude "architectury.common.json"
}

tasks.named("shadowJar") {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archiveFile)
    archiveClassifier.set("")
    injectAccessWidener.set(true)
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":Common").sourcesJar
    dependsOn(commonSources)
    from commonSources.archiveFile.map { zipTree(it) }
}
