println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))

allprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "idea"

    archivesBaseName = "${modName.replaceAll("[^a-zA-Z]", "")}"
    version = "v${modVersion}-${libs.versions.minecraft.get()}-${project.name}"
    group = modMavenGroup

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "Sponge"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven {
            name = "Jared"
            url = "https://maven.blamejared.com/"
        }
        maven {
            name = "Jitpack"
            url = "https://jitpack.io"
        }
        maven {
            name = "Shedaniel"
            url = "https://maven.shedaniel.me/"
        }
        maven {
            name = "Parchment"
            url = "https://maven.parchmentmc.org"
        }
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
        maven {
            name = "Wisp Forest"
            url "https://maven.wispforest.io/releases"
        }
        maven {
            name = "Su5eD"
            url "https://maven.su5ed.dev/releases"
        }
        maven {
            name = "AppleSkin"
            url "https://maven.ryanliptak.com/"
        }
        maven {
            name = "Minecraft Forge"
            url "https://maven.minecraftforge.net/"
        }
        flatDir {
            dirs "libs"
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = "CurseForge"
                    url = "https://cursemaven.com"
                }
            }
            filter {
                includeGroup "curse.maven"
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        // If Javadoc is generated, this must be specified in that task too.
        options.encoding = "UTF-8"
        options.release = libs.versions.java.get() as Integer
        options.warnings = false
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(libs.versions.java.get())
        withSourcesJar()
        withJavadocJar()
    }

    // silence missing javadoc comments, we just don't care
    javadoc.options.addStringOption("Xdoclint:none", "-quiet")

    tasks.withType(Jar).configureEach {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE

        from rootProject.file("../LICENSE.md")
        from rootProject.file("../LICENSE-ASSETS.md")
        from rootProject.file("../LICENSE-FORGE.md")
        from rootProject.file("../LICENSE-NIGHT-CONFIG.md")
        from rootProject.file("../LICENSING.md")
        from rootProject.file("CHANGELOG.md")

        manifest {
            attributes([
                    "Specification-Title"           : modName,
                    "Specification-Version"         : modVersion,
                    "Specification-Vendor"          : modAuthor,
                    "Implementation-Title"          : modName,
                    "Implementation-Version"        : modVersion,
                    "Implementation-Vendor"         : modAuthor,
                    "Implementation-Timestamp"      : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "Implementation-Timestamp-Milli": System.currentTimeMillis(),
                    "Implementation-URL"            : modSourceUrl,
                    "Built-On-Java"                 : "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})",
                    "Built-On-Minecraft"            : libs.versions.minecraft.get()
            ])
        }

        group "jar"
    }

    tasks.withType(GenerateModuleMetadata) {
        // Disables Gradle's custom module metadata from being published to maven. The
        // metadata includes mapped dependencies which are not reasonably consumable by
        // other mod developers.
        enabled = false
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "me.hypherionmc.cursegradle"
    apply plugin: "com.modrinth.minotaur"

    loom {
        silentMojangMappingsLicense()

        mixin {
            useLegacyMixinAp = true
            defaultRefmapName = "${modId}.${project.name.toLowerCase()}.refmap.json"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${libs.versions.minecraft.get()}"
        mappings loom.layered() {
            officialMojangMappings {
                setNameSyntheticMembers(true)
            }
            parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.version.get()}@zip")
        }
    }

    processResources {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE

        from(project(":Common").sourceSets.main.resources) {
            exclude "${modId}.common.mixins.json", "common.mixins.json"
        }

        from(project(":Common").file("src/generated/resources")) {
            exclude ".cache/"
        }

        var fileNames = [
                "^common.mixins.json\$": "${modId}.common.mixins.json",
                "^fabric.mixins.json\$": "${modId}.fabric.mixins.json",
                "^neoforge.mixins.json\$": "${modId}.neoforge.mixins.json",
                "^forge.mixins.json\$": "${modId}.forge.mixins.json"
        ]

        fileNames.forEach {from, to -> rename from, to }

        var resourceProperties = [
                "modId": modId,
                "modName": modName,
                "modVersion": modVersion,
                "modDescription": modDescription,
                "modGroup": project.group,
                "modPageUrl": modSourceUrl,
                "modIssueUrl": modIssueUrl,
                "modUpdateUrl": modUpdateUrl,
                "modAuthor": modAuthor,
                "modLicense": modLicense,
                "minFabricVersion": libs.versions.hasProperty("fabricloader") && libs.versions.fabricloader.hasProperty("min") ? libs.versions.fabricloader.min.get() : "1",
                "minFabricApiVersion": libs.versions.hasProperty("fabricapi") && libs.versions.fabricapi.hasProperty("min") ? libs.versions.fabricapi.min.get() : "1",
                "minForgeFMLVersion": "1",
                "minForgeVersion": libs.versions.hasProperty("minecraftforge") && libs.versions.minecraftforge.hasProperty("min") ? libs.versions.minecraftforge.min.get() : "1",
                "minNeoForgeFMLVersion": "1",
                "minNeoForgeVersion": libs.versions.hasProperty("neoforge") && libs.versions.neoforge.hasProperty("min") ? libs.versions.neoforge.min.get() : "1",
                "minecraftVersion": libs.versions.minecraft.get(),
                "minPuzzlesVersion": libs.versions.hasProperty("puzzleslib") && libs.versions.puzzleslib.hasProperty("min") ? libs.versions.puzzleslib.min.get() : "1",
                "resourcePackFormat": libs.versions.resources.get(),
                "dataPackFormat": libs.versions.data.get(),
                "mainEntryPoint": "${project.group}.fabric.${archivesBaseName}Fabric",
                "clientEntryPoint": "${project.group}.fabric.client.${archivesBaseName}FabricClient",
                "modFabricEnvironment": modFabricEnvironment,
                "modForgeDisplayTest": modForgeDisplayTest
        ]

        inputs.properties resourceProperties

        var matchingFiles = [
                "pack.mcmeta",
                "fabric.mod.json",
                "META-INF/neoforge.mods.toml",
                "META-INF/mods.toml",
                modId + ".common.mixins.json",
                modId + ".fabric.mixins.json",
                modId + ".neoforge.mixins.json",
                modId + ".forge.mixins.json",
                "common.mixins.json",
                "fabric.mixins.json",
                "neoforge.mixins.json",
                "forge.mixins.json"
        ]

        filesMatching(matchingFiles) {
            expand resourceProperties + [project: project]
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "${modId}-${project.name.toLowerCase()}"
                version = modVersion
                from components.java
                pom {
                    name = "${modName} [${project.name}]"
                    description = "${modDescription}"
                    url = "${modSourceUrl}"
                    scm {
                        url = "${modSourceUrl}"
                        connection = "${modSourceUrl}".replace("https", "scm:git:git").concat(".git")
                        developerConnection = "${modSourceUrl}".replace("https://github.com/", "scm:git:git@github.com:").concat(".git")
                    }
                    issueManagement {
                        system = "github"
                        url = "${modIssueUrl}"
                    }
                    licenses {
                        license {
                            name = "${modLicense}"
                            url = "https://spdx.org/licenses/${modLicense}.html"
                        }
                    }
                    developers {
                        developer {
                            id = "${modAuthor}".toLowerCase()
                            name = "${modAuthor}"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "FuzsModResources"
                url "file://" + project.hasProperty("modResources") ? "${project.findProperty("modResources")}/maven" : System.getenv("local_maven")
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    curseforge {
        if (!file("../CHANGELOG.md").canRead()) {
            throw new FileNotFoundException("Could not read changelog file")
        }
        apiKey = project.hasProperty("curseApiToken") ? project.findProperty("curseApiToken") : ""
        project {
            id = projectCurseForgeId
            changelogType = "markdown"
            changelog = file("../CHANGELOG.md")
            releaseType = projectReleaseType
            addGameVersion project.name
            addGameVersion libs.versions.minecraft.get()
            mainArtifact(remapJar) {
                displayName = "[${project.name.toUpperCase()}] [${libs.versions.minecraft.get()}] ${archivesBaseName}-v${modVersion}"
                var requiredDependencies = "dependenciesRequired${project.name}CurseForge"
                var embeddedDependencies = "dependenciesEmbedded${project.name}CurseForge"
                var optionalDependencies = "dependenciesOptional${project.name}CurseForge"
                if (project.hasProperty(requiredDependencies) || project.hasProperty(embeddedDependencies) || project.hasProperty(optionalDependencies)) {
                    relations {
                        if (project.hasProperty(requiredDependencies)) {
                            (project.findProperty(requiredDependencies) as String).split(",").each {
                                requiredDependency it.trim()
                            }
                        }
                        if (project.hasProperty(embeddedDependencies)) {
                            (project.findProperty(embeddedDependencies) as String).split(",").each {
                                embeddedLibrary it.trim()
                            }
                        }
                        if (project.hasProperty(optionalDependencies)) {
                            (project.findProperty(optionalDependencies) as String).split(",").each {
                                optionalDependency it.trim()
                            }
                        }
                    }
                }
            }
        }
        options {
//            debug = true
            javaVersionAutoDetect = false
            forgeGradleIntegration = false
            fabricIntegration = false
            detectFabricApi = false
        }
    }

    modrinth {
        if (!file("../CHANGELOG.md").canRead()) {
            throw new FileNotFoundException("Could not read changelog file")
        }
        token = project.hasProperty("modrinthPersonalAccessToken") ? project.findProperty("modrinthPersonalAccessToken") : ""
        projectId = projectModrinthId
        versionNumber = project.version
        versionName = "[${project.name.toUpperCase()}] [${libs.versions.minecraft.get()}] ${archivesBaseName}-v${modVersion}"
        changelog = file("../CHANGELOG.md").text
        versionType = projectReleaseType
        uploadFile = remapJar
        loaders.add project.name.toLowerCase()
        gameVersions.add libs.versions.minecraft.get()
        var requiredDependencies = "dependenciesRequired${project.name}Modrinth"
        var embeddedDependencies = "dependenciesEmbedded${project.name}Modrinth"
        var optionalDependencies = "dependenciesOptional${project.name}Modrinth"
        if (project.hasProperty(requiredDependencies) || project.hasProperty(embeddedDependencies) || project.hasProperty(optionalDependencies)) {
            dependencies {
                if (project.hasProperty(requiredDependencies)) {
                    (project.findProperty(requiredDependencies) as String).split(",").each {
                        required.project it.trim()
                    }
                }
                if (project.hasProperty(embeddedDependencies)) {
                    (project.findProperty(embeddedDependencies) as String).split(",").each {
                        embedded.project it.trim()
                    }
                }
                if (project.hasProperty(optionalDependencies)) {
                    (project.findProperty(optionalDependencies) as String).split(",").each {
                        optional.project it.trim()
                    }
                }
            }
        }
//	    debugMode = true
    }

    task refreshUpdateJson {
        onlyIf { project.hasProperty("modResources") }
        doLast {
            var updateFile = file(project.findProperty("modResources").concat(File.separator).concat("update").concat(File.separator).concat("${modId}").concat(".json"))
            var updateJson
            if (updateFile.exists() && updateFile.canRead()) {
                updateJson = new groovy.json.JsonSlurper().parseText(updateFile.text)
                updateJson["promos"]["${libs.versions.minecraft.get()}-latest"] = "${modVersion}"
                // alpha and beta releases will contain "a" or "b" char respectively, don"t update recommended for those
                if ("${modVersion}".matches("[^a-zA-Z]+")) {
                    updateJson["promos"]["${libs.versions.minecraft.get()}-recommended"] = "${modVersion}"
                }
            } else {
                var builder = new groovy.json.JsonBuilder()
                updateJson = builder {
                    homepage "${modSourceUrl}"
                    promos {
                        "${libs.versions.minecraft.get()}-latest" "${modVersion}"
                        // alpha and beta releases will contain "a" or "b" char respectively, don"t update recommended for those
                        if ("${modVersion}".matches("[^a-zA-Z]+")) {
                            "${libs.versions.minecraft.get()}-recommended" "${modVersion}"
                        }
                    }
                }
            }
            var output = new groovy.json.JsonOutput()
            updateFile.write(output.prettyPrint(output.toJson(updateJson)))
        }
    }

    task copyJarToDir(type: Copy) {
        onlyIf { project.hasProperty("buildJarOutputDir") && project.hasProperty("uniqueBuildNumber") }
        if (project.findProperty("copyBuildJar").toBoolean()) {
            from remapJar
            into project.findProperty("buildJarOutputDir")
            // add build number to be able to distinguish jars when testing thorough official launcher
            // build number is stored in global gradle.properties
            rename { fileName -> fileName.replace("v${modVersion}", "v${modVersion}.${uniqueBuildNumber}") }
        }
    }

    task incrementBuildNumber {
        onlyIf { project.hasProperty("uniqueBuildNumber") }
        doLast {
            var propertiesName = "gradle.properties"
            // build number is stored in global gradle.properties
            var propertiesFile = new File(project.gradle.gradleUserHomeDir, propertiesName)
            if (!propertiesFile.canRead()) { throw new FileNotFoundException("Could not read file ".concat(propertiesName)) }
            var buildNumberMatcher = java.util.regex.Pattern.compile("uniqueBuildNumber=(\\d+)").matcher(propertiesFile.getText())
            buildNumberMatcher.find()
            var versionCode = Integer.parseInt(buildNumberMatcher.group(1))
            var propertiesContent = buildNumberMatcher.replaceAll("uniqueBuildNumber=" + ++versionCode)
            propertiesFile.write(propertiesContent)
        }
    }

    build.finalizedBy project.tasks.copyJarToDir, project.tasks.incrementBuildNumber
}

architectury {
    minecraft = libs.versions.minecraft.get()
}

apply from: "https://raw.githubusercontent.com/Fuzss/modresources/main/gradle/${libs.versions.minecraft.get()}/tasks.gradle"
